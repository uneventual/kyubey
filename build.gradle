buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.4'
  }
}

plugins {
  id 'java'
  id 'application'
  id 'idea'
  id 'com.github.johnrengelman.shadow' version '7.0.0'
  id "com.google.protobuf" version "0.9.4"
  id 'org.jetbrains.kotlin.jvm'
}

group = 'fr.pierrezemb'
version = '1.0.0-SNAPSHOT'

repositories {
  mavenCentral()
}

ext {
  vertxVersion = '4.0.3'
  // junitJupiterEngineVersion = '5.13.0-M3'
  junitJupiterEngineVersion = '5.4.0'
}

application {
  mainClassName = 'io.vertx.core.Launcher'
}
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}


def mainVerticleName = 'fr.pierrezemb.fdb.layer.etcd.MainVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

dependencies {
  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2")
  implementation "io.vertx:vertx-junit5:$vertxVersion"
  implementation "io.vertx:vertx-grpc:$vertxVersion"
  // https://mvnrepository.com/artifact/org.foundationdb/fdb-record-layer-core
  implementation("org.foundationdb:fdb-record-layer-core:4.2.8.0")
  implementation 'org.slf4j:slf4j-api:1.7.36'

  // implementation 'org.foundationdb:fdb-record-layer-core-pb3:2.8.91.0'

  implementation group: 'io.grpc', name: 'grpc-alts', version: "1.35.0"

  implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

  testImplementation "io.vertx:vertx-junit5:$vertxVersion"
  testImplementation "org.testcontainers:testcontainers:1.15.3"
  testImplementation "io.etcd:jetcd-core:0.5.0"

  // https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
  testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'

  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterEngineVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterEngineVersion"
  testImplementation("org.junit.jupiter:junit-jupiter-params:$junitJupiterEngineVersion")
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}


shadowJar {
  archiveClassifier = 'fat'
  manifest {
    attributes 'Main-Verticle': mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events 'PASSED', 'FAILED', 'SKIPPED'
  }
  dependsOn cleanTest
  testLogging {
    showStandardStreams = true
    exceptionFormat = 'full'
  }
}

run {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.2.0'
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:1.35.0"
    }
    vertx {
      artifact = "io.vertx:vertx-grpc-protoc-plugin:${vertxVersion}"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc
      vertx
    }
  }
}

processResources {
    dependsOn generateProto
}

tasks.withType(JavaCompile) {
  options.compilerArgs << '-Xlint:unchecked'
}
